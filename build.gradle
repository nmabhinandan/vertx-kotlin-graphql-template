buildscript {
    ext {
        kotlin_version = '1.2.71'
        vertx_version = '3.5.3'
        moshi_version = '1.7.0'
    }

    repositories {
        jcenter()
        mavenCentral()
    }

    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

plugins {
    id 'java'
    id 'application'
    id 'com.github.johnrengelman.shadow' version '1.2.3'
}

apply plugin: 'kotlin'
apply plugin: 'kotlin-kapt'

sourceCompatibility = '1.8'

mainClassName = 'io.vertx.core.Launcher'
def mainVerticleName = 'main.MainVerticle'

repositories {
    jcenter()
    mavenCentral()
}

tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).all {
    kotlinOptions {
        jvmTarget = "1.8"
    }
}

dependencies {
    compile "io.vertx:vertx-core:$vertx_version"
    compile "io.vertx:vertx-lang-kotlin:$vertx_version"
    compile "io.vertx:vertx-web:$vertx_version"
    compile "io.vertx:vertx-web-api-contract:$vertx_version"

    compile "com.squareup.moshi:moshi:$moshi_version"
    compile "com.squareup.moshi:moshi-kotlin:$moshi_version"
    kapt "com.squareup.moshi:moshi-kotlin-codegen:$moshi_version"

    compile 'org.slf4j:slf4j-simple:1.7.25'
    compile 'io.github.microutils:kotlin-logging:1.6.10'

    compile "com.graphql-java:graphql-java:10.0"
    compile "com.graphql-java-kickstart:graphql-java-tools:5.3.3"

    compile "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
    compile "org.jetbrains.kotlin:kotlin-runtime:$kotlin_version"
}

// Redeploy watcher.
run {
    args = ['run', mainVerticleName,
            "--launcher-class=$mainClassName",
            "--redeploy=src/**/*.*",
            "--on-redeploy=./gradlew classes"
    ]
}

// Naming and packaging settings for the "shadow jar".
shadowJar {
    baseName = 'app'
    classifier = 'shadow'

    manifest {
        attributes 'Main-Verticle': mainVerticleName
    }
    mergeServiceFiles {
        include 'META-INF/services/io.vertx.core.spi.VerticleFactory'
    }
}

// Heroku relies on the 'stage' task to deploy.
task stage {
    dependsOn shadowJar
}
